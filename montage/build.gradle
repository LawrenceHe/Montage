apply plugin: 'com.android.library'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://maven.google.com' }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$gradle_version"
    }
}

repositories {
    jcenter()
    mavenLocal()
    maven { url 'https://maven.google.com' }
}

android {
    compileSdkVersion Integer.parseInt(System.properties['compileSdkVersion'])
    buildToolsVersion System.properties['buildToolsVersion']

    defaultConfig {
        minSdkVersion Integer.parseInt(System.properties['minSdkVersion'])
        targetSdkVersion Integer.parseInt(System.properties['targetSdkVersion'])

        // Support ARouter
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

}

configurations.all {
    // check for updates every build
    resolutionStrategy.cacheChangingModulesFor 60, 'seconds'
}

dependencies {
    if (project.hasProperty('vlayoutDevMode')) {
        println 'compile with vlayout source code'
        api(project(':vlayout')) {
            exclude group: 'com.android.support', module: 'recyclerview-v7'
            exclude group: 'com.android.support', module: 'support-v4'
            exclude group: 'com.android.support', module: 'support-annotations'
        }
    } else {
        api('com.alibaba.android:vlayout:1.2.24@aar') {
            changing = true
        }
    }

    if (project.hasProperty('ultraViewPagerDevMode')) {
        println 'compile with ultraViewPager source code'
        api project(':ultraviewpager')
    } else {
        api 'com.alibaba.android:ultraviewpager:1.0.7.8@aar'
    }

    compileOnly 'io.reactivex.rxjava2:rxjava:2.1.12'
    compileOnly 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation project(':common')
    api project(':virtualview')
    annotationProcessor "com.alibaba:arouter-compiler:$arouter_compiler_version"
}
